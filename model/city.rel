module flight_graph

    doc"""
        City(x in Entity)
    
    Type check relation for the `City` entity.
    """
    def City(x) =
        lookup_city(_, _, x)

    doc"""
        lookup_city(
            county in County,
            city_name in String,
            x in City
        )
    
    Determines whether the entity `x` represents the city with name `city_name` located in `county`. 
    Note: City names are not unique, so they are identified relative to the county.
    """
    def lookup_city(county in County, city_name in String, x in Entity) =
        airport:city(loc, city_name) and
        airport:county(loc, county_name) and
        airport:state(loc, state_code) and
        abbr(state, state_code) and
        name(county, county_name) and
        ^City(county, city_name, x)
        from loc, state, state_code, county_name

    doc"""
        name(city in City, s in String)
    
    Relates the entity `city` to its name `s`. Note that city names are not unique.
    """
    def name(city in City, s in String) =
        lookup_city(_, s, city)

    doc"""
        located_in(city in City, county in County)
    
    Specifies that the `city` is located in `county`.
    """
    def located_in(city in City, county in County) =
        lookup_city(county, _, city)

end
